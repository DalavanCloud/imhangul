AC_INIT(imhangul.c)

AM_INIT_AUTOMAKE(imhangul, 0.9.10)
AC_CONFIG_HEADERS(config.h)
AM_MAINTAINER_MODE

dnl Checks for programs
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL

AC_DISABLE_STATIC
AC_PROG_RANLIB 
AC_PROG_LIBTOOL

dnl Checks for libraries.
dnl AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
PKG_CHECK_MODULES(GTK, gtk+-2.0 >= 2.2.0,,
		  AC_MSG_ERROR([im-hangul needs GTK+ 2.2.0 or higher]))

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([libintl.h locale.h string.h])

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE

dnl Checks for library functions.

# gettext stuff
ALL_LINGUAS="ko"
AM_GLIB_GNU_GETTEXT

GETTEXT_PACKAGE="im-hangul"
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", gettext package name)

dnl check for gtk binary version
GTK_BINARY_VERSION=`$PKG_CONFIG --variable=gtk_binary_version gtk+-2.0`
GTK_PREFIX=`$PKG_CONFIG --variable=prefix gtk+-2.0`
AC_SUBST(GTK_BINARY_VERSION)
AC_SUBST(GTK_PREFIX)

dnl check for sysconfdir
IM_HANGUL_GTK_IMMODULE_FILE="${sysconfdir}/gtk-2.0/gtk.immodules"
ac_save_CFLAGS="$CFLAGS"
ac_save_LIBS="$LIBS"
CFLAGS="$CFLAGS $GTK_CFLAGS"
LIBS="$GTK_LIBS $LIBS"
rm -f conftest.msg
AC_RUN_IFELSE([AC_LANG_SOURCE([[
#include <stdio.h>
#include <stdlib.h>
#include <gtk/gtk.h>
int main(void) {
    gchar *str;
	FILE *file;
	unsetenv("GTK_IM_MODULE_FILE");
	str = gtk_rc_get_im_module_file();
	printf("checking for GTK_IM_MODULE_FILE... ");
	file = fopen("conftest.msg", "w");
	if (file) {
		fprintf(file, "%s", str);
		printf("%s\n", str);
		fclose(file);
		g_free(str);
		return 0;
	} else {
		g_free(str);
		printf("\n");
		return 1;
	}
}]])],
[IM_HANGUL_GTK_IMMODULE_FILE=`cat conftest.msg`]
)
rm -f conftest.msg
CFLAGS="$ac_save_CFLAGS"
LIBS="$ac_save_LIBS"
AC_SUBST(IM_HANGUL_GTK_IMMODULE_FILE)

dnl default keyboard
AC_ARG_WITH(default-keyboard, [  --with-default-keyboard=2/39/3f   default hangul keyboard])
case "$with_default_keyboard" in
    2)
        AC_DEFINE_UNQUOTED(DEFAULT_KEYBOARD_2, "ko",
                           [Define default hangul keyboard])
        AC_DEFINE_UNQUOTED(DEFAULT_KEYBOARD_39, "",
                           [Define default hangul keyboard])
        AC_DEFINE_UNQUOTED(DEFAULT_KEYBOARD_3F, "",
                           [Define default hangul keyboard])
        ;;
    39)
        AC_DEFINE_UNQUOTED(DEFAULT_KEYBOARD_2, "",
                           [Define default hangul keyboard])
        AC_DEFINE_UNQUOTED(DEFAULT_KEYBOARD_39, "ko",
                           [Define default hangul keyboard])
        AC_DEFINE_UNQUOTED(DEFAULT_KEYBOARD_3F, "",
                           [Define default hangul keyboard])
        ;;
    3f)
        AC_DEFINE_UNQUOTED(DEFAULT_KEYBOARD_2, "",
                           [Define default hangul keyboard])
        AC_DEFINE_UNQUOTED(DEFAULT_KEYBOARD_39, "",
                           [Define default hangul keyboard])
        AC_DEFINE_UNQUOTED(DEFAULT_KEYBOARD_3F, "ko",
                           [Define default hangul keyboard])
        ;;
    *)
        AC_DEFINE_UNQUOTED(DEFAULT_KEYBOARD_2, "ko",
                           [Define default hangul keyboard])
        AC_DEFINE_UNQUOTED(DEFAULT_KEYBOARD_39, "",
                           [Define default hangul keyboard])
        AC_DEFINE_UNQUOTED(DEFAULT_KEYBOARD_3F, "",
                           [Define default hangul keyboard])
        ;;
esac

AC_OUTPUT([
Makefile
po/Makefile.in
imhangul.spec
])
