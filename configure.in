AC_INIT([imhangul], [2.1.1], [choe.hwanjin@gmail.com], [imhangul])

AC_CONFIG_SRCDIR([.])
AC_CONFIG_HEADERS(config.h)
AM_INIT_AUTOMAKE([dist-bzip2])

dnl Checks for programs
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AM_PROG_CC_C_O

AC_DISABLE_STATIC
AC_PROG_LIBTOOL

dnl Checks for libraries.
dnl AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
PKG_CHECK_MODULES(GTK, gtk+-2.0 >= 2.2.0,,
		  AC_MSG_ERROR([im-hangul needs GTK+ 2.2.0 or higher]))

PKG_CHECK_MODULES(LIBHANGUL, libhangul >= 0.0.12,,
		  AC_MSG_ERROR([im-hangul needs libhangul 0.0.12 or higher]))

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([libintl.h locale.h string.h])

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE

dnl Checks for library functions.

# gettext stuff
ALL_LINGUAS="ko"
AM_GLIB_GNU_GETTEXT

GETTEXT_PACKAGE="im-hangul"
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", gettext package name)

dnl check for gtk binary version
# GTK_BINARY_VERSION=`$PKG_CONFIG --variable=gtk_binary_version gtk+-2.0`
# AC_SUBST(GTK_BINARY_VERSION)
GTK_PREFIX=`$PKG_CONFIG --variable=prefix gtk+-2.0`
AC_SUBST(GTK_PREFIX)
GTK_IM_MODULE_DIR=$GTK_PREFIX/lib/gtk-2.0/immodules
AC_ARG_WITH(gtk-im-module-dir, [  --with-gtk-im-module-dir=DIR gtk im module files dir])
if test "$with_gtk_im_module_dir" ; then 
    GTK_IM_MODULE_DIR="$with_gtk_im_module_dir"
fi
AC_SUBST(GTK_IM_MODULE_DIR)

dnl check for sysconfdir
GTK_IM_MODULE_FILE="${sysconfdir}/gtk-2.0/gtk.immodules"
AC_ARG_WITH(gtk-im-module-file, [  --with-gtk-im-module-file=FILE gtk im modules file])
if test "$with_gtk_im_module_file" ; then
    GTK_IM_MODULE_FILE="$with_gtk_im_module_file"
else
    ac_save_CFLAGS="$CFLAGS"
    ac_save_LIBS="$LIBS"
    CFLAGS="$CFLAGS $GTK_CFLAGS"
    LIBS="$GTK_LIBS $LIBS"
    rm -f conftest.msg
    echo -n "checking for GTK_IM_MODULE_FILE... "
    AC_RUN_IFELSE([AC_LANG_SOURCE([[
    #include <stdio.h>
    #include <stdlib.h>
    #include <gtk/gtk.h>
    int main(void) {
	gchar *str;
	    FILE *file;
	    unsetenv("GTK_IM_MODULE_FILE");
	    str = gtk_rc_get_im_module_file();
	    file = fopen("conftest.msg", "w");
	    if (file) {
		    fprintf(file, "%s", str);
		    fclose(file);
		    g_free(str);
		    return 0;
	    } else {
		    g_free(str);
		    return 1;
	    }
    }]])],
    [GTK_IM_MODULE_FILE=`cat conftest.msg`]
    )
    echo "${GTK_IM_MODULE_FILE}"
    rm -f conftest.msg
    CFLAGS="$ac_save_CFLAGS"
    LIBS="$ac_save_LIBS"
    AC_SUBST(GTK_IM_MODULE_FILE)
fi 

dnl default keyboard
AC_ARG_WITH(default-keyboard, [  --with-default-keyboard=2/39/3f   default hangul keyboard])
case "$with_default_keyboard" in
    2)
	DEFAULT_KEYBOARD="2 beol"
        AC_DEFINE_UNQUOTED(DEFAULT_KEYBOARD_ID, "2",
                           [Define default hangul keyboard])
        ;;
    39)
	DEFAULT_KEYBOARD="3 beol 390"
        AC_DEFINE_UNQUOTED(DEFAULT_KEYBOARD_ID, "39",
                           [Define default hangul keyboard])
        ;;
    3f)
	DEFAULT_KEYBOARD="3 beol final"
        AC_DEFINE_UNQUOTED(DEFAULT_KEYBOARD_ID, "3f",
                           [Define default hangul keyboard])
        ;;
    *)
	DEFAULT_KEYBOARD="2 beol"
        AC_DEFINE_UNQUOTED(DEFAULT_KEYBOARD_ID, "2",
                           [Define default hangul keyboard])
        ;;
esac

AC_CONFIG_FILES([
Makefile
po/Makefile.in
imhangul.spec
])

AC_OUTPUT

echo ""
echo "configuration:"
echo "	GTK_IM_MODULE_DIR:  $GTK_IM_MODULE_DIR"
echo "	GTK_IM_MODULE_FILE: $GTK_IM_MODULE_FILE"
echo "	DEFAULT KEYBOARD:   $DEFAULT_KEYBOARD"
